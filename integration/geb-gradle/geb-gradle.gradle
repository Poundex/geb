plugins {
    id 'geb.published-groovy-module'
    id 'geb.spock-2-module'
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish' version '0.16.0'
}

repositories {
    mavenCentral()
}

dependencies {
    groovy localGroovy()
    implementation gradleApi()

    implementation libs.guava

    testImplementation libs.spock.core
}

publishing.publications.main.pom {
    name = "Geb Gradle plugins"
    description = "Gradle plugins for Geb (pronounced \"jeb\")."
}

pluginBundle {
    website = "https://gebish.org/manual/current/#gradle-plugins"
    vcsUrl = "https://github.com/geb/geb"
    tags = ["geb", "testing", "cloud", "browser"]
}

gradlePlugin {
    plugins {
        sauceLabsPlugin {
            id = 'org.gebish.saucelabs'
            displayName = "Geb Sauce Labs plugin"
            description = "A plugin that simplifies running Geb tests at Sauce Labs"
            implementationClass = 'geb.gradle.saucelabs.SaucePlugin'
        }
        browserStackPlugin {
            id = 'org.gebish.browserstack'
            displayName = "Geb BrowserStack plugin"
            description = "A plugin that simplifies running Geb tests at BrowserStack"
            implementationClass = 'geb.gradle.browserstack.BrowserStackPlugin'
        }
        lambdaTestPlugin {
            id = 'org.gebish.lambdatest'
            displayName = "Geb LambdaTest plugin"
            description = "A plugin that simplifies running Geb tests at LambdaTest"
            implementationClass = 'geb.gradle.lambdatest.LambdaTestPlugin'
        }
    }
}

tasks.named("publishJarsAndManual") {
    if (!gebModule.isSnapshot()) {
        dependsOn 'publishPlugins'
    }
}