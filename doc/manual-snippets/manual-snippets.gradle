configurations {
    realBrowserTestCompile.extendsFrom testCompile
    realBrowserTestRuntime.extendsFrom testRuntime

    webdriverBinariesProperties
}

dependencies {
    testCompile project(':internal:test-support-spock')
    testCompile project(':internal:system-properties')
    testCompile project(':module:geb-spock')
    testCompile 'commons-fileupload:commons-fileupload:1.3.1'
    testCompile 'com.github.stefanbirkner:system-rules:1.19.0'

    webdriverBinariesProperties project(path: ":", configuration: "webdriverBinariesProperties")
}

sourceSets {
    realBrowserTest
}

compileRealBrowserTestGroovy {
    classpath += sourceSets.test.output.classesDirs
}

task copyWebdriverBinariesProperties(type: Copy) {
    from configurations.webdriverBinariesProperties
    into "${buildDir}/webdriver-binaries"
}

task realBrowserTest(type: Test) {
    testClassesDirs = sourceSets.realBrowserTest.output.classesDirs
    classpath = sourceSets.realBrowserTest.runtimeClasspath + sourceSets.test.output.classesDirs

    mustRunAfter test
}

check.dependsOn realBrowserTest

test {
    inputs.property("geckodriverVersion", geckodriverVersion)

    maxParallelForks availableProcessors

    dependsOn copyWebdriverBinariesProperties
}

idea.module {
    testSourceDirs += sourceSets.realBrowserTest.allSource.srcDirs
    scopes.TEST.plus += [configurations.realBrowserTestCompile]
}
