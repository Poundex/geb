plugins {
    id 'org.asciidoctor.jvm.convert' version '3.3.0'
}

archivesBaseName = "geb-manual"

repositories {
    jcenter()
}

configurations {
    manual

    delegate.default.extendsFrom manual
}

asciidoctorj {
    options = [doctype: 'book']
    attributes = [
        toc                    : 'left',
        encoding               : 'utf-8',
        'test-dir'             : '../../../../manual-snippets/src/test/groovy',
        'test-resources-dir'   : '../../../../manual-snippets/src/test/resources',
        'real-browser-test-dir': '../../../../manual-snippets/real-browser/src/test/groovy',
        'core-source-dir'      : '../../../../../module/geb-core/src/main/groovy',
        'core-test-dir'        : '../../../../../module/geb-core/src/test/groovy',
        numbered               : true,
        sectlinks              : true,
        idprefix               : '',
        idseparator            : '-',
        icons                  : 'font',
        'source-highlighter'   : 'coderay',
        'geb-version'          : project.version,
        'geb-group'            : project.group,
        'geb-group-dir'        : project.group.replace('.', '/'),
        'spock-version'        : '0.7',
        'spock-core-version'   : spockVersion,
        'selenium-version'     : seleniumVersion,
        'groovy-version'       : groovyVersion,
        'home-page'            : 'www.gebish.org',
        docinfo1               : true,
        linkcss                : true,
        stylesheet             : 'css/style.css',
        imagesdir              : 'images'
    ]

    docExtensions project(":doc:asciidoctor-extension")
}

asciidoctor {
    sources {
        include 'index.adoc'
    }

    resources {
        from file('src/resources')
    }

    baseDirFollowsSourceDir()
}

task apiDoc(type: Groovydoc) {
    def apiGroovyModuleProjects = apiGroovyModules.collect { project(it) }
    def apiGroovyModulesClasspath = files(apiGroovyModuleProjects*.configurations*.compileClasspath)
    groovyClasspath = new GroovyRuntime(project).inferGroovyClasspath(apiGroovyModulesClasspath)
    classpath = files()
    destinationDir = file("$buildDir/apiDoc")
    apiGroovyModuleProjects.each {
        source it.sourceSets.main.groovy, it.sourceSets.main.java
        classpath += it.sourceSets.main.compileClasspath + it.sourceSets.main.output
    }
}

task packageManual(type: Sync) {
    into "$buildDir/manual"
    from asciidoctor

    into("api") {
        from apiDoc
    }

    eachFile { file ->
        if (file.path.startsWith("html5")) {
            file.relativePath = new RelativePath(!file.directory, file.relativePath.segments[1..-1] as String[])
        }
    }

    includeEmptyDirs = false
}

task manualZip(type: Zip) {
    from packageManual
}

artifacts {
    manual manualZip
}

publishing {
    publications {
        manual(MavenPublication) {
            artifactId 'geb-manual'
            artifact manualZip
            pom {
                name = "Geb Manual - The Book of Geb"
                description = "Geb (pronounced \"jeb\") manual, user guide and API reference."
            }
        }
    }
}

task publishJarsAndManual {
    dependsOn 'publishManualPublicationToSonatypeRepository'
}

build {
    dependsOn manualZip
}

task openManual() {
    dependsOn tasks.asciidoctor
    doLast {
        new java.awt.Desktop().browse(fileTree(tasks.asciidoctor.outputDir).find { it.name == 'index.html' }.toURI())
    }
}

asciidoctor {
    inputs.files(
        { project(":doc:manual-snippets").sourceSets.test.allSource },
        { project(":doc:manual-snippets:real-browser").sourceSets.test.allSource },
        { project(":module:geb-core").sourceSets.main.allSource },
        { project(":module:geb-core").sourceSets.test.allSource }
    ).withPathSensitivity(PathSensitivity.RELATIVE)
}

idea {
    module {
        excludeDirs += file('build')
    }
}