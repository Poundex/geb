import geb.gradle.EnvironmentVariablesCommandLineArgumentProvider
import org.ysb33r.grolifant.api.OperatingSystem

import static java.lang.Runtime.runtime

buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.21.1"
        classpath "gradle.plugin.com.github.erdi:idea-gradle-plugins:2.1"
        classpath "gradle.plugin.com.energizedwork.asl2:asl2-gradle-plugin:1.1"
        classpath "gradle.plugin.com.github.erdi.webdriver-binaries:webdriver-binaries-gradle-plugin:2.4"
    }
}

apply plugin: 'base'
apply plugin: 'io.codearte.nexus-staging'
apply plugin: 'com.energizedwork.asl2'
apply plugin: 'com.github.erdi.extended-idea'
apply from: "gradle/idea.gradle"

def getProperty(String name, defaultValue) {
    hasProperty(name) ? property(name) : defaultValue
}

nexusStaging {
    username = getProperty("sonatypeOssUsername", null)
    password = getProperty("sonatypeOssPassword", null)
    numberOfRetries = 60
    delayBetweenRetriesInMillis = 2000
}

allprojects { project ->
    version = '4.1-SNAPSHOT'
    group = 'org.gebish'

    ext {
        isSnapshot = version.endsWith("-SNAPSHOT")
        isCi = project.hasProperty("ci")
        availableProcessors = isCi ? 2 : Math.min(runtime.availableProcessors().intdiv(2), 4)

        seleniumVersion = "3.141.59"
        htmlUnitDriverVersion = "2.35.1"
        groovyVersion = "2.5.14"
        spockVersion = "1.3-groovy-2.5"
        junit4Version = "4.12"
        jettyVersion = "9.4.8.v20171121"
        threeTenExtraVersion = "1.4"

        spockDependency = dependencies.create("org.spockframework:spock-core:$spockVersion") {
            exclude module: "groovy-all"
        }

        seleniumDependency = "org.seleniumhq.selenium:selenium-api:$seleniumVersion"
        seleniumSupportDependency = "org.seleniumhq.selenium:selenium-support:$seleniumVersion"
        htmlUnitDriverDependency = "org.seleniumhq.selenium:htmlunit-driver:$htmlUnitDriverVersion"

        jsoupDependency = "org.jsoup:jsoup:1.11.2"

        sauceConnectDependency = "com.saucelabs:ci-sauce:1.141"

        repositories {
            mavenCentral()
        }

        groovyModules = [
            ":module:geb-core", ":internal:test-support", ":internal:test-support-spock", ":module:geb-spock",
            ":module:geb-junit", ":module:geb-junit4", ":module:geb-junit5", ":module:geb-testng",
            ":module:geb-implicit-assertions", ":module:geb-ast", ":module:geb-waiting", ":module:geb-exceptions",
            ":integration:geb-gradle", ":doc:site", ":doc:manual-snippets", ":doc:asciidoctor-extension"
        ]

        publishedModules = [
            ":module:geb-core", ":module:geb-implicit-assertions", ":module:geb-ast", ":module:geb-waiting",
            ":module:geb-exceptions", ":module:geb-spock", ":module:geb-junit", ":module:geb-junit4",
            ":module:geb-junit5", ":module:geb-testng", ":doc:manual", ":integration:geb-gradle"
        ]

        dockerisedCrossBrowserTestModules = [":module:geb-core", ":module:geb-spock", ":module:geb-junit4"]

        publishedGroovyModules = groovyModules.findAll { it in publishedModules }
        apiGroovyModules = publishedGroovyModules - ":module:geb-implicit-assertions" - ":integration:geb-gradle"
    }

    if (isCi) {
        plugins.withType(ReportingBasePlugin) {
            def projectDirPath = project.path.substring(1).replaceAll(":", "/")
            def copyTask = task("copyReportsIntoRoot", type: Copy) {
                into "${rootProject.buildDir}/reports/${projectDirPath}"
                from reporting.baseDir
            }
            tasks.withType(Reporting) { reportingTask ->
                reportingTask.finalizedBy copyTask
            }
        }
    }
}

tasks.create('codenarc')

subprojects {
    if (path in groovyModules) {
        apply plugin: "groovy"
        apply plugin: "java-library"
        apply plugin: "idea"
        apply plugin: "codenarc"

        configurations {
            groovy

            compile.extendsFrom groovy
            codenarc.extendsFrom groovy
        }

        pluginManager.withPlugin("maven") {
            conf2ScopeMappings.addMapping(0, configurations.groovy, Conf2ScopeMappingContainer.COMPILE)
        }

        dependencies {
            groovy "org.codehaus.groovy:groovy:${groovyVersion}"
            groovy "org.codehaus.groovy:groovy-templates:${groovyVersion}"
            codenarc 'org.codenarc:CodeNarc:2.0.0', {
                exclude module: "GMetrics"
                exclude group: "org.codehaus.groovy"
            }
        }

        codenarc.configFile = rootProject.file('gradle/codenarc/rulesets.groovy')

        project.tasks.withType(CodeNarc) { codenarcSourceSetTask ->
            codenarcSourceSetTask.exclude 'GebConfig*.groovy'
            rootProject.tasks.codenarc.dependsOn codenarcSourceSetTask
            rootProject.allprojects {
                tasks.withType(Test) {
                    mustRunAfter codenarcSourceSetTask
                }
            }
        }

        tasks.withType(Test) {
            def gebReportsDir = reporting.file("geb/$name")
            outputs.dir(gebReportsDir)
            jvmArgumentProviders.add(
                new EnvironmentVariablesCommandLineArgumentProvider('geb.build.reportsDir': gebReportsDir.absolutePath)
            )

            systemProperty 'jna.nosys', 'true'
        }

    }

    if (path in publishedGroovyModules) {
        task sourcesJar(type: Jar) {
            classifier 'sources'
            from sourceSets.main.allSource
        }

        task javadocJar(type: Jar) {
            classifier 'javadoc'
            from groovydoc
        }

        artifacts {
            archives javadocJar, sourcesJar
        }

        gradle.taskGraph.whenReady { taskGraph ->
            if (taskGraph.hasTask(uploadArchives) && !isSnapshot) {
                tasks.withType(JavaCompile) {
                    options.compilerArgs << "-Werror" << "-Xlint:options"
                }
                tasks.withType(GroovyCompile) {
                    options.compilerArgs << "-Werror" << "-Xlint:options"
                }
            }
        }
    }

    if (path in dockerisedCrossBrowserTestModules) {
        task chromeLinuxTest(type: Test) {
            maxHeapSize = "512m"
            systemProperty "geb.dockerized.driver", "chrome"
        }

        task allDockerisedCrossBrowserTests {
            dependsOn chromeLinuxTest
        }
    }

    tasks.withType(Groovydoc) {
        exclude "**/internal/**"

        link "http://docs.oracle.com/javase/8/docs/api/",
            "java.net",
            "java.util",
            "java.lang",
            "java.io"

        link "http://static.javadoc.io/org.seleniumhq.selenium/selenium-api/$seleniumVersion",
            "org.openqa.selenium"

        link "http://static.javadoc.io/org.spockframework/spock-core/$spockVersion",
            "spock.lang"

        link "http://static.javadoc.io/junit/junit/$junit4Version",
            "org.junit"

        link "http://static.javadoc.io/org.threeten/threeten-extra/$threeTenExtraVersion",
            "org.threeten.extra"

        link "http://docs.groovy-lang.org/docs/groovy-${groovyVersion}/html/api/",
            "groovy.lang"

        def docName = project.name == "manual" ? "Geb" : project.name
        docTitle = "Groovy API Documentation for $docName $version"
        footer = "<p style='text-align: center'>Groovy API Documentation for $docName $version - Licensed under the Apache License, Version 2.0 - <a href='http://www.gebish.org'>http://www.gebish.org</a></p>"
        windowTitle = "Groovy API for $docName $version"
    }

    tasks.withType(Test) {
        maxHeapSize = "768m"
        jvmArgs '-XX:MaxMetaspaceSize=192m'
        testLogging {
            exceptionFormat = 'full'
        }
        inputs.property("isWindows", OperatingSystem.current().windows)
    }

    tasks.withType(GroovyCompile) {
        groovyOptions.forkOptions.memoryMaximumSize = '256m'
    }


    if (path in publishedModules) {
        apply plugin: "base"
        apply plugin: "maven"
        apply plugin: "signing"

        signing {
            sign configurations.archives
            required { !isSnapshot && !isCi && gradle.taskGraph.hasTask(uploadArchives) }
        }

        apply from: "$rootDir/gradle/pom.gradle"

        uploadArchives { task ->
            repositories.mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
                gradle.taskGraph.whenReady { taskGraph ->
                    if (taskGraph.hasTask(task)) {
                        repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                            authentication(userName: sonatypeOssUsername, password: sonatypeOssPassword)
                        }
                        snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                            authentication(userName: sonatypeOssUsername, password: sonatypeOssPassword)
                        }
                    }
                }
            }

            task.onlyIf { isSnapshot || !isCi }
        }
    }

    if (isCi) {
        tasks.withType(Test) {
            testLogging {
                showStackTraces = true
                exceptionFormat "full"
            }
        }
    }

}

task release {
    dependsOn { allprojects.findAll { it.path in publishedModules }*.uploadArchives }
}

task stage(type: Sync) {
    dependsOn 'doc:site:prepareDeployment'
}
