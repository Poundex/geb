import java.time.Duration

buildscript {
    repositories {
        mavenCentral()
        gradlePluginPortal()
    }

    dependencies {
        classpath "gradle.plugin.com.github.erdi.webdriver-binaries:webdriver-binaries-gradle-plugin:2.5"
    }
}

plugins {
    id 'geb.coordinates'
    id "io.github.gradle-nexus.publish-plugin" version "1.0.0"
    id "com.energizedwork.asl2" version "1.1"
    id "com.github.erdi.extended-idea" version "2.1"
}

nexusPublishing {
    repositories {
        sonatype {
            stagingProfileId = "c1802e5c2fe49"
            username.set(providers.gradleProperty("sonatypeOssUsername").forUseAtConfigurationTime())
            password.set(providers.gradleProperty("sonatypeOssPassword").forUseAtConfigurationTime())
        }
    }

    clientTimeout = Duration.ofSeconds(300)
    connectTimeout = Duration.ofSeconds(60)

    transitionCheckOptions {
        maxRetries.set(60)
        delayBetween.set(Duration.ofMillis(3000))
    }
}

task stage {
    dependsOn 'doc:site:prepareDeployment'
}

ideaProjectComponents { components ->
    components.file "gradle/idea/codeStyle.xml"
    components.file "gradle/idea/codeStyleGroovy.xml"
}

idea {
    project {
        jdkName "1.8"

        wildcards += ['good-conf.groovy', 'gebScript*.groovy']
    }

    extended {
        workspace {
            junit {
                tasks = [':integration:geb-gradle:classes', ':integration:geb-gradle:pluginUnderTestMetadata']
            }
        }
    }
}
