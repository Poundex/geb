import geb.gradle.EnvironmentVariablesCommandLineArgumentProvider
import org.ysb33r.grolifant.api.OperatingSystem

import java.time.Duration

import static java.lang.Runtime.runtime

buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath "io.github.gradle-nexus:publish-plugin:1.0.0"
        classpath "gradle.plugin.com.github.erdi:idea-gradle-plugins:2.1"
        classpath "gradle.plugin.com.energizedwork.asl2:asl2-gradle-plugin:1.1"
        classpath "gradle.plugin.com.github.erdi.webdriver-binaries:webdriver-binaries-gradle-plugin:2.4"
    }
}

apply plugin: 'base'
apply plugin: 'io.github.gradle-nexus.publish-plugin'
apply plugin: 'com.energizedwork.asl2'
apply plugin: 'com.github.erdi.extended-idea'
apply from: "gradle/idea.gradle"

def getProperty(String name, defaultValue) {
    hasProperty(name) ? property(name) : defaultValue
}


allprojects { project ->
    version = '4.2-SNAPSHOT'
    group = 'org.gebish'

    ext {
        isSnapshot = version.endsWith("-SNAPSHOT")
        isCi = project.hasProperty("ci")
        availableProcessors = isCi ? 2 : Math.min(runtime.availableProcessors().intdiv(2), 4)

        seleniumVersion = "3.141.59"
        htmlUnitDriverVersion = "2.35.1"
        groovyVersion = "2.5.14"
        spockVersion = "1.3-groovy-2.5"
        junit4Version = "4.13.2"
        junit5Version = "5.7.0"
        jettyVersion = "9.4.8.v20171121"
        threeTenExtraVersion = "1.4"

        spockDependency = dependencies.create("org.spockframework:spock-core:$spockVersion") {
            exclude module: "groovy-all"
        }

        seleniumDependency = "org.seleniumhq.selenium:selenium-api:$seleniumVersion"
        seleniumSupportDependency = "org.seleniumhq.selenium:selenium-support:$seleniumVersion"
        htmlUnitDriverDependency = "org.seleniumhq.selenium:htmlunit-driver:$htmlUnitDriverVersion"

        jsoupDependency = "org.jsoup:jsoup:1.11.2"

        sauceConnectDependency = "com.saucelabs:ci-sauce:1.141"

        repositories {
            mavenCentral()
        }

        groovyModules = [
            ":module:geb-core", ":internal:test-support", ":internal:test-support-spock", ":module:geb-spock",
            ":module:geb-junit", ":module:geb-junit4", ":module:geb-junit5", ":module:geb-testng",
            ":module:geb-implicit-assertions", ":module:geb-ast", ":module:geb-waiting", ":module:geb-exceptions",
            ":integration:geb-gradle", ":doc:site", ":doc:manual-snippets", ":doc:manual-snippets:fixtures",
            ":doc:manual-snippets:real-browser", ":doc:asciidoctor-extension"
        ]

        publishedModules = [
            ":module:geb-core", ":module:geb-implicit-assertions", ":module:geb-ast", ":module:geb-waiting",
            ":module:geb-exceptions", ":module:geb-spock", ":module:geb-junit", ":module:geb-junit4",
            ":module:geb-junit5", ":module:geb-testng", ":doc:manual", ":integration:geb-gradle"
        ]

        dockerisedCrossBrowserTestModules = [":module:geb-core", ":module:geb-spock", ":module:geb-junit4"]

        publishedGroovyModules = groovyModules.findAll { it in publishedModules }
        apiGroovyModules = publishedGroovyModules - ":module:geb-implicit-assertions" - ":integration:geb-gradle"
    }

    if (isCi) {
        plugins.withType(ReportingBasePlugin) {
            def projectDirPath = project.path.substring(1).replaceAll(":", "/")
            def copyTask = task("copyReportsIntoRoot", type: Copy) {
                into "${rootProject.buildDir}/reports/${projectDirPath}"
                from reporting.baseDir
            }
            tasks.withType(Reporting) { reportingTask ->
                reportingTask.finalizedBy copyTask
            }
        }
    }
}

tasks.create('codenarc')

nexusPublishing {
    repositories {
        sonatype {
            stagingProfileId = "c1802e5c2fe49"
            username.set(providers.gradleProperty("sonatypeOssUsername").forUseAtConfigurationTime())
            password.set(providers.gradleProperty("sonatypeOssPassword").forUseAtConfigurationTime())
        }
    }

    clientTimeout = Duration.ofSeconds(300)
    connectTimeout = Duration.ofSeconds(60)

    transitionCheckOptions {
        maxRetries.set(60)
        delayBetween.set(Duration.ofMillis(3000))
    }
}

subprojects {
    if (path in groovyModules) {
        apply plugin: "groovy"
        apply plugin: "java-library"
        apply plugin: "idea"
        apply plugin: "codenarc"

        configurations {
            groovy

            compile.extendsFrom groovy
            codenarc.extendsFrom groovy
        }

        dependencies {
            groovy "org.codehaus.groovy:groovy:${groovyVersion}"
            groovy "org.codehaus.groovy:groovy-templates:${groovyVersion}"
            codenarc 'org.codenarc:CodeNarc:2.0.0', {
                exclude module: "GMetrics"
                exclude group: "org.codehaus.groovy"
            }
        }

        codenarc.configFile = rootProject.file('gradle/codenarc/rulesets.groovy')

        project.tasks.withType(CodeNarc) { codenarcSourceSetTask ->
            codenarcSourceSetTask.exclude 'GebConfig*.groovy'
            rootProject.tasks.codenarc.dependsOn codenarcSourceSetTask
            rootProject.allprojects {
                tasks.withType(Test) {
                    mustRunAfter codenarcSourceSetTask
                }
            }
        }

        tasks.withType(Test) {
            def gebReportsDir = reporting.file("geb/$name")
            outputs.dir(gebReportsDir)
            jvmArgumentProviders.add(
                new EnvironmentVariablesCommandLineArgumentProvider('geb.build.reportsDir': gebReportsDir.absolutePath)
            )

            systemProperty 'jna.nosys', 'true'
        }

    }

    if (path in publishedGroovyModules) {
        task javadocJar(type: Jar) {
            classifier 'javadoc'
            from groovydoc
        }

        plugins.withId("maven-publish") {
            java {
                withJavadocJar()
                withSourcesJar()
            }

            publishing {
                publications {
                    main(MavenPublication) {
                        from components.java
                    }
                }
            }
        }

        task publishJarsAndManual {
            dependsOn 'publishMainPublicationToSonatypeRepository'
        }
    }

    if (path in dockerisedCrossBrowserTestModules) {
        task chromeLinuxTest(type: Test) {
            maxHeapSize = "512m"
            systemProperty "geb.dockerized.driver", "chrome"
        }

        task allDockerisedCrossBrowserTests {
            dependsOn chromeLinuxTest
        }
    }

    tasks.withType(Groovydoc) {
        exclude "**/internal/**"

        link "http://docs.oracle.com/javase/8/docs/api/",
            "java.net",
            "java.util",
            "java.lang",
            "java.io"

        link "http://static.javadoc.io/org.seleniumhq.selenium/selenium-api/$seleniumVersion",
            "org.openqa.selenium"

        link "http://static.javadoc.io/org.spockframework/spock-core/$spockVersion",
            "spock.lang"

        link "http://static.javadoc.io/org.junit.jupiter/junit-jupiter-api/$junit5Version",
            "org.junit.jupiter.api"

        link "http://static.javadoc.io/junit/junit/$junit4Version",
            "org.junit"

        link "http://static.javadoc.io/org.threeten/threeten-extra/$threeTenExtraVersion",
            "org.threeten.extra"

        link "http://docs.groovy-lang.org/docs/groovy-${groovyVersion}/html/api/",
            "groovy.lang"

        def docName = project.name == "manual" ? "Geb" : project.name
        docTitle = "Groovy API Documentation for $docName $version"
        footer = "<p style='text-align: center'>Groovy API Documentation for $docName $version - Licensed under the Apache License, Version 2.0 - <a href='http://www.gebish.org'>http://www.gebish.org</a></p>"
        windowTitle = "Groovy API for $docName $version"
    }

    tasks.withType(Test) {
        maxHeapSize = "768m"
        jvmArgs '-XX:MaxMetaspaceSize=192m'
        testLogging {
            exceptionFormat = 'full'
        }
        inputs.property("isWindows", OperatingSystem.current().windows)
    }

    tasks.withType(GroovyCompile) {
        groovyOptions.forkOptions.memoryMaximumSize = '256m'
    }


    if (path in publishedModules) {
        apply plugin: "base"
        apply plugin: "maven-publish"
        apply plugin: "signing"

        signing {
            required { isSnapshot && !isCi }
            publishing.publications.configureEach {
                sign(it)
            }
        }

        publishing {
            publications.configureEach {
                pom {
                    url = "http://www.gebish.org"
                    inceptionYear = "2010"
                    licenses {
                        license {
                            name = 'The Apache Software License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            distribution = 'repo'
                        }
                    }
                    scm {
                        url = "https://github.com/geb/geb/"
                    }
                    developers {
                        developer {
                            id = "alkemist"
                            name = "Luke Daley"
                            roles = ["Founder"]
                        }
                        developer {
                            id = "erdi"
                            name = "Marcin Erdmann"
                            roles = ["Lead"]
                        }
                    }
                }
            }
        }

        tasks.withType(PublishToMavenRepository) {
            onlyIf { isSnapshot || !isCi }
        }
    }

    if (isCi) {
        tasks.withType(Test) {
            testLogging {
                showStackTraces = true
                exceptionFormat "full"
            }
        }
    }

}

task stage(type: Sync) {
    dependsOn 'doc:site:prepareDeployment'
}
