import geb.gradle.EnvironmentVariablesCommandLineArgumentProvider

import java.time.Duration

import static java.lang.Runtime.runtime

buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath "io.github.gradle-nexus:publish-plugin:1.0.0"
        classpath "gradle.plugin.com.github.erdi:idea-gradle-plugins:2.1"
        classpath "gradle.plugin.com.energizedwork.asl2:asl2-gradle-plugin:1.1"
        classpath "gradle.plugin.com.github.erdi.webdriver-binaries:webdriver-binaries-gradle-plugin:2.5"
    }
}

apply plugin: 'base'
apply plugin: 'io.github.gradle-nexus.publish-plugin'
apply plugin: 'com.energizedwork.asl2'
apply plugin: 'com.github.erdi.extended-idea'
apply from: "gradle/idea.gradle"

def getProperty(String name, defaultValue) {
    hasProperty(name) ? property(name) : defaultValue
}


allprojects { project ->
    version = '5.1-SNAPSHOT'
    group = 'org.gebish'

    ext {
        isSnapshot = version.endsWith("-SNAPSHOT")
        isCi = project.hasProperty("ci")
        availableProcessors = isCi ? 2 : Math.min(runtime.availableProcessors().intdiv(2), 4)

        seleniumVersion = "3.141.59"
        htmlUnitDriverVersion = "2.35.1"
        groovyVersion = "3.0.8"
        spockVersion = "2.0-groovy-3.0"
        junit4Version = "4.13.2"
        junit5Version = "5.7.0"
        jettyVersion = "9.4.8.v20171121"
        threeTenExtraVersion = "1.4"

        spockDependency = dependencies.create("org.spockframework:spock-core:$spockVersion")

        seleniumDependency = "org.seleniumhq.selenium:selenium-api:$seleniumVersion"
        seleniumSupportDependency = "org.seleniumhq.selenium:selenium-support:$seleniumVersion"
        htmlUnitDriverDependency = "org.seleniumhq.selenium:htmlunit-driver:$htmlUnitDriverVersion"

        jsoupDependency = "org.jsoup:jsoup:1.11.2"

        sauceConnectDependency = "com.saucelabs:ci-sauce:1.153"

        repositories {
            mavenCentral()
        }

        groovy3Modules = [
            ":module:geb-core", ":internal:test-support", ":internal:test-support-spock", ":module:geb-spock",
            ":module:geb-junit", ":module:geb-junit4", ":module:geb-junit5", ":module:geb-testng",
            ":module:geb-implicit-assertions", ":module:geb-ast", ":module:geb-waiting", ":module:geb-exceptions",
            ":doc:manual-snippets", ":doc:manual-snippets:fixtures", ":doc:manual-snippets:real-browser",
            ":doc:asciidoctor-extension"
        ]

        spock2Modules = groovy3Modules + [":integration:geb-gradle"]

        groovyModules = groovy3Modules + [":integration:geb-gradle", ":doc:site"]

        publishedModules = [
            ":module:geb-core", ":module:geb-implicit-assertions", ":module:geb-ast", ":module:geb-waiting",
            ":module:geb-exceptions", ":module:geb-spock", ":module:geb-junit", ":module:geb-junit4",
            ":module:geb-junit5", ":module:geb-testng", ":doc:manual", ":integration:geb-gradle"
        ]

        dockerisedCrossBrowserTestModules = [":module:geb-core", ":module:geb-spock", ":module:geb-junit4"]

        publishedGroovyModules = groovyModules.findAll { it in publishedModules }
        apiGroovyModules = publishedGroovyModules - ":module:geb-implicit-assertions" - ":integration:geb-gradle"
    }
}

tasks.create('codenarc')

nexusPublishing {
    repositories {
        sonatype {
            stagingProfileId = "c1802e5c2fe49"
            username.set(providers.gradleProperty("sonatypeOssUsername").forUseAtConfigurationTime())
            password.set(providers.gradleProperty("sonatypeOssPassword").forUseAtConfigurationTime())
        }
    }

    clientTimeout = Duration.ofSeconds(300)
    connectTimeout = Duration.ofSeconds(60)

    transitionCheckOptions {
        maxRetries.set(60)
        delayBetween.set(Duration.ofMillis(3000))
    }
}

subprojects {
    if (path in groovyModules) {
        apply plugin: "groovy"
        apply plugin: "java-library"
        apply plugin: "idea"
        apply plugin: "codenarc"

        configurations {
            groovy

            api.extendsFrom groovy
            codenarc.extendsFrom groovy
        }

        dependencies {
            codenarc 'org.codenarc:CodeNarc:2.0.0', {
                exclude module: "GMetrics"
                exclude group: "org.codehaus.groovy"
            }
        }

        codenarc.configFile = rootProject.file('gradle/codenarc/rulesets.groovy')

        project.tasks.withType(CodeNarc) { codenarcSourceSetTask ->
            codenarcSourceSetTask.exclude 'GebConfig*.groovy'
            rootProject.tasks.codenarc.dependsOn codenarcSourceSetTask
            rootProject.allprojects {
                tasks.withType(Test) {
                    mustRunAfter codenarcSourceSetTask
                }
            }
        }

        tasks.withType(Test) {
            def gebReportsDir = reporting.file("geb/$name")
            outputs.dir(gebReportsDir)
            jvmArgumentProviders.add(
                new EnvironmentVariablesCommandLineArgumentProvider('geb.build.reportsDir': gebReportsDir.absolutePath)
            )

            afterSuite { desc, result ->
                if (!desc.parent) {
                    if (result.testCount == 0) {
                        throw new IllegalStateException("No tests were found")
                    }
                }
            }

            systemProperty 'jna.nosys', 'true'
        }

        tasks.withType(GroovyCompile) {
            groovyOptions.forkOptions.memoryMaximumSize = '256m'
        }
    }

    if (path in groovy3Modules) {
        dependencies {
            groovy platform("org.codehaus.groovy:groovy-bom:$groovyVersion")
            groovy "org.codehaus.groovy:groovy"
            groovy "org.codehaus.groovy:groovy-templates"
        }
    }

    if (path in spock2Modules) {
        tasks.withType(Test) {
            useJUnitPlatform()
        }
    }

    if (path in publishedGroovyModules) {
        task javadocJar(type: Jar) {
            classifier 'javadoc'
            from groovydoc
        }

        plugins.withId("maven-publish") {
            java {
                withJavadocJar()
                withSourcesJar()
            }

            publishing {
                publications {
                    main(MavenPublication) {
                        from components.java
                    }
                }
            }
        }

        task publishJarsAndManual {
            dependsOn 'publishMainPublicationToSonatypeRepository'
        }
    }

    if (path in dockerisedCrossBrowserTestModules) {
        task chromeLinuxTest(type: Test) {
            maxHeapSize = "512m"
            systemProperty "geb.dockerized.driver", "chrome"
        }

        task allDockerisedCrossBrowserTests {
            dependsOn chromeLinuxTest
        }
    }

    tasks.withType(Groovydoc) {
        exclude "**/internal/**"

        link "http://docs.oracle.com/javase/8/docs/api/",
            "java.net",
            "java.util",
            "java.lang",
            "java.io"

        link "http://static.javadoc.io/org.seleniumhq.selenium/selenium-api/$seleniumVersion",
            "org.openqa.selenium"

        link "http://static.javadoc.io/org.spockframework/spock-core/$spockVersion",
            "spock.lang"

            link "http://static.javadoc.io/org.junit.jupiter/junit-jupiter-api/$junit5Version",
                "org.junit.jupiter.api"

            link "http://static.javadoc.io/junit/junit/$junit4Version",
                "org.junit"

        link "http://static.javadoc.io/org.threeten/threeten-extra/$threeTenExtraVersion",
            "org.threeten.extra"

        link "http://docs.groovy-lang.org/docs/groovy-${groovyVersion}/html/api/",
            "groovy.lang"

        def docName = project.name == "manual" ? "Geb" : project.name
        docTitle = "Groovy API Documentation for $docName $version"
        footer = "<p style='text-align: center'>Groovy API Documentation for $docName $version - Licensed under the Apache License, Version 2.0 - <a href='http://www.gebish.org'>http://www.gebish.org</a></p>"
        windowTitle = "Groovy API for $docName $version"
    }
}

task stage(type: Sync) {
    dependsOn 'doc:site:prepareDeployment'
}
